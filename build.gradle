/*
 *     Copyright (C) 2018  Hyperium <https://hyperium.cc/>
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU Lesser General Public License as published
 *     by the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU Lesser General Public License for more details.
 *
 *     You should have received a copy of the GNU Lesser General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

// For those who want the bleeding edge
buildscript {
    ext.kotlin_version = '1.2.61'
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'https://jitpack.io'
        }

    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

apply plugin: 'net.minecraftforge.gradle.tweaker-client'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'kotlin'
apply plugin: 'java'


sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

version = "1.0"
group = "cc.hyperium"
archivesBaseName = "Hyperium"

minecraft {
    version = "1.8.9"
    tweakClass = "cc.hyperium.launch.HyperiumTweaker"
    mappings = "stable_20"
    runDir = 'run'
    clientJvmArgs = ["-XX:-DisableExplicitGC"] // fast world loading
    makeObfSourceJar = false
}
dependencies {

    embed('org.spongepowered:mixin:0.7.7-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        //exclude module: 'log4j-core'
    }
    embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    embed "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    embed 'com.github.Vatuu:discord-rpc:1.6.1'

    embed 'com.github.FalseHonesty:reflections:855f40b80b'
    embed 'com.github.ben-manes.caffeine:caffeine:2.6.2'

    embed group: 'com.google.guava', name: 'guava', version: '17.0'
    embed group: 'com.google.code.gson', name: 'gson', version: '2.8.2'

    embed group: 'org.slick2d', name: 'slick2d-core', version: '1.0.2'
    embed group: 'com.nativelibs4java', name: 'bridj', version: '0.7.0'

    embed "com.github.KevinPriv:MojangAPI:1.0"
    embed 'com.github.Cubxity:DeobfStack:1.2'
    embed 'com.github.HyperiumClient:Installer:1.8'

    embed fileTree(dir: 'libs', include: 'Netty Client.jar')
    compile fileTree(dir: 'libs', include: '*.jar')

}

mixin {
    // Specify "notch" or "searge" here
    defaultObfuscationEnv notch
    add sourceSets.main, "mixin.hyperium.refmap.json"
}

repositories {
    maven { url "https://jitpack.io" }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven/'
    }
    mavenCentral()
    jcenter()
}

processResources {
    inputs.files "src/main/resources"
    outputs.dir "build/classes/main"
    copy {
        from("src/main/resources")
        into("build/classes/main")
    }
}

jar {
    dependsOn configurations.compile
    from {
        configurations.embed.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

jar {
    manifest.attributes(
            //"MixinConfigs": 'mixins.hyperium.json, mixins.ctjs.json',
            "MixinConfigs": 'mixins.hyperium.json',
            "TweakClass": 'cc.hyperium.launch.HyperiumTweaker',
            "TweakOrder": 0,
            "Manifest-Version": 1.0,
            'Main-Class': 'cc.hyperium.installer.InstallerStart'
    )
    configurations.embed.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
            exclude 'org/jetbrains/annotations', 'org/jetbrains/annotations/**'
        }
    }
}
